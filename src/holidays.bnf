<holidays> = <comment>? include? holiday+
holiday = !comment-marker !include-marker name <'|'> holiday-definition <newline>

<name> = #'[^\n|]+'

<holiday-definition> = ddmmm ddmmm-opts? | ddmmmyyyy | nth-day-of-week nth-day-of-week-opts? | expression expression-opts?

ddmmm = day-31 month-31 | day-30 month-30 | day-feb month-feb
<day-31> = day-30 | '31'
<day-30> = day-feb | '30'
<day-feb> = <'0'?> non-zero-digit |
            '10' | '11' | '12' | '13' | '14' | '15' | '16' | '17' | '18' | '19' |
            '20' | '21' | '22' | '23' | '24' | '25' | '26' | '27' | '28' | '29'
<non-zero-digit> = '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
<digit> = '0' | non-zero-digit
<month-31> = 'Jan' | 'Mar' | 'May' | 'Jul' | 'Aug' | 'Oct' | 'Dec'
<month-30> = 'Apr' | 'Jun' | 'Sep' | 'Nov'
<month-feb> = 'Feb'

ddmmm-opts = observation-rule? limit-clause? | limit-clause? observation-rule?
observation-rule = <'|'> observation-rule-option
<observation-rule-option> = 'observed' | 'observed-monday-tuesday'
<limit-clause> = start-year | end-year
start-year = <'|'> #'\d{4}' <'->'>
end-year = <'|'> <'->'> #'\d{4}'

ddmmmyyyy = ddmmm yyyy exception-marker?
yyyy = #'\d{4}'

nth-day-of-week = day-of-week-nth day-of-week any-month
<day-of-week-nth> = '1' | '2' | '3' | '4' | '5' | '-1' | '-2' | '-3' | '-4' | '-5'
<day-of-week> = 'Sun' | 'Mon' | 'Tue' | 'Wed' | 'Thu' | 'Fri' | 'Sat'
<any-month> = month-31 | month-30 | month-feb

nth-day-of-week-opts = limit-clause?

expression = easter

easter = 'E' expression-operator expression-operand
<expression-operator> = '+' | '-'
<expression-operand> = #'\d+'

expression-opts = observation-rule? limit-clause? | limit-clause? observation-rule?

exception-marker = '-'

comment = comment-marker #".*" newline
comment-marker = ';'

include = <include-marker> <'include'> <#"\s+"> include-name <newline>
include-marker = '#'
<include-name> = #'[a-zA-Z][-a-zA-Z]+'

newline = '\n' | '\r\n'
